#!/usr/bin/perl -w
#
# net_udp - Munin plugin for graphing UDP queue and drops
# Copyright (C) 2019 CESNET, z.s.p.o.
#
# Author: Jan Tomášek <jan@tomasek.cz>
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.

use strict;
use warnings;
use Socket;
use Sys::Syslog qw(:standard :macros);
use Data::Dumper;

my $file = '/proc/net/udp';

my $items = {
	     tx_queue => {
			  index => 6,
			  name  => 'Transmit Queue',
			  type  => 'DERIVE',
			  draw  => 'LINE1',
			  min   => 0,
			 },
	     rx_queue => {
			  index => 7,
			  name  => 'Receive Queue',
			  type  => 'DERIVE',
			  draw  => 'LINE1',
			  min   => 0,
			 },
	     drops   => {
			 index => 16,
			 name  => 'Drops',
			 type  => 'DERIVE',
			 draw  => 'LINE1',
			 min   => 0,
			},
	    };
my $totals = { map {$_ => 0} keys %{$items}};

openlog('net_udp', 'ndelay,pid', LOG_DAEMON);

my $localIP = 1;
my $localPort = 2;
my $remoteIP = 3;
my $remotePort = 4;

sub convertIP {
  my $IP = shift;
  $IP = inet_ntoa(pack('V', hex $IP)); # V is litle endian format used by Intel CPU

  return $IP;
};

sub syslog_line {
  my $line = shift;

  syslog('LOG_INFO',
         sprintf('tx_queue=%d; rx_queue=%d; drop=%d; local addr=%s:%d; remote addr=%s:%d',
	         $line->[$items->{tx_queue}->{index}],
	         $line->[$items->{rx_queue}->{index}],
		 $line->[$items->{drops}->{index}],
		 convertIP($line->[$localIP]), hex($line->[$localPort]),
		 convertIP($line->[$remoteIP]), hex($line->[$remotePort])))
}



if ( defined($ARGV[0]) and $ARGV[0] eq 'config' ) {
  print "graph_title UDP queue sizes and drops
graph_vlabel packets per second
graph_category network\n";
  foreach my $item (sort keys %{$items}) {
    print "$item.label ".$items->{$item}->{name}."\n";
    print "$item.type ". $items->{$item}->{type}."\n";
    print "$item.min ".  $items->{$item}->{min}."\n";
  };
  exit;
};

open(NU, "<$file") or die "Failed to read from $file: $?";
while (my $line = <NU>) {
  chomp ($line);
  $line =~ s/:/ /g;
  $line =~ s/\s{1,}/ /g;
  $line =~ s/^\s//g;

  next if ($line =~ /^sl/);

  my @line = split(/ /, $line);
  my $non_zero = 0;
  foreach my $item (keys %{$items}) {
    my $item_value = $line[$items->{$item}->{index}];
    $totals->{$item} += $item_value;
    if ($item ne 'drops') {
      # drops is increasing counter so the first droped packet will
      # cause this script to syslog until connection is droped. I
      # assume that for debuging should be enought to log when rx/tx
      # queue is non_zero.
      $non_zero += $item_value;
    };
  };

  if ($non_zero) {
    syslog_line(\@line);
  };
};

foreach my $item (sort keys %{$items}) {
  print "$item.value ".$totals->{$item}."\n";
};

closelog;
